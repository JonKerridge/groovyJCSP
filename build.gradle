plugins {
  id 'groovy'
  id 'java-library'
  id 'maven-publish'
}

group 'jonkerridge'
version '1.1.9'
// prior versions on bintray
// version 1.1.8 github release
// version 1.1.9 github package

repositories {
  mavenCentral()
//  mavenLocal() for testing
  maven { // use this to download the CSPforJAVA.jcsp library
    name = "GitHubPackages"
    url = "https://maven.pkg.github.com/CSPforJAVA/jcsp"
    // must have read artifacts credentials with GitHub in order to download
    // even though the repository is publicly accessible
    credentials {
      username = project.findProperty("gpr.user")
      password = project.findProperty("gpr.key")
    }
  }
}


task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set( "sources")
}

dependencies {
  implementation 'cspforjava:jcsp:1.1.9'
  implementation 'org.codehaus.groovy:groovy-all:3.0.7'
  implementation group: 'junit', name: 'junit', version: '4.13.1'
}

artifacts {
  archives sourcesJar
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/JonKerridge/groovyJCSP"
      credentials {
        username = project.findProperty("gpr.user")
        password = project.findProperty("gpr.key")
      }
    }
  }
  publications {
    groovyJCSP(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}